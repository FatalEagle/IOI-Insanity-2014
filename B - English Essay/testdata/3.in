             <essay> ::= <paragraph> { <paragraph> }
         <paragraph> ::= <sentence> "." { " " <sentence> "." } <EOL>
          <sentence> ::= <noun-phrase> " " <verb-phrase> |
                         <sentence> " " <conjunction> " " <sentence>
       <noun-phrase> ::= <pronoun> |
                         <noun> |
                         <article> " " <noun> |
                         <noun-phrase> " " <preposition-phrase> |
                         <noun-phrase> " " <relative-clause>
       <verb-phrase> ::= <verb> |
                         <verb-phrase> " " <noun-phrase> |
                         <verb-phrase> " " <adjective> |
                         <verb-phrase> " " <adverb> |
                         <verb-phrase> " " <preposition-phrase>
<preposition-phrase> ::= <preposition> " " <noun-phrase>
   <relative-clause> ::= <determiner> " " <verb-phrase>
              <noun> ::= cat | hat | mice | mouse
              <verb> ::= am | are | is | eats | turn
         <adjective> ::= hungry | smelly | cool | happy
            <adverb> ::= right | left | quickly | slowly
           <pronoun> ::= I | me | you | it
           <article> ::= the | a | an
       <preposition> ::= to | in | on | near
       <conjunction> ::= and | or | but
        <determiner> ::= that | which | who
